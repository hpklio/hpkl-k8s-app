open module hpkl.common.k8s.Affinities

import "@k8s/api/core/v1/PodSpec.pkl"

class PodAntiAffinityConfig {
  preset: "Soft"| "Hard" | "" = ""
  topologyKey: String = "kubernetes.io/hostname"
  selectorLabels: Mapping<String, String>
}

podAntiAffinityConfig: PodAntiAffinityConfig = new {}

config: PodSpec.Affinity = new {
  when (podAntiAffinityConfig.preset != "") {
    podAntiAffinity = new {
      when (podAntiAffinityConfig.preset == "Soft") {
        preferredDuringSchedulingIgnoredDuringExecution {
          new {
            weight = 1
            podAffinityTerm {
              labelSelector {
                matchLabels = podAntiAffinityConfig.selectorLabels
              }
              topologyKey = podAntiAffinityConfig.topologyKey
            }
          }
        }
      }
      when (podAntiAffinityConfig.preset == "Hard") {
        requiredDuringSchedulingIgnoredDuringExecution {
          new {
            labelSelector {
              matchLabels = podAntiAffinityConfig.selectorLabels
            }
            topologyKey = podAntiAffinityConfig.topologyKey
          }
        }
      }
    }
  }
}
